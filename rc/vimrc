if &compatible
  set nocompatible               " Be iMproved
endif

function! s:source_rc(path, ...) abort
  let use_global = get(a:000, 0, !has('vim_starting'))
  let abspath = resolve(expand('~/.config/nvim/rc/' . a:path))
  if !use_global
    execute 'source' fnameescape(abspath)
    return
  endif

  " substitute all 'set' to 'setglobal'
  let content = map(readfile(abspath),
        \ 'substitute(v:val, "^\\W*\\zsset\\ze\\W", "setglobal", "")')
  " create tempfile and source the tempfile
  let tempfile = tempname()
  try
    call writefile(content, tempfile)
    execute 'source' fnameescape(tempfile)
  finally
    if filereadable(tempfile)
      call delete(tempfile)
    endif
  endtry
endfunction

set rtp+=/usr/local/share/nvim/runtime/

let g:vim_bootstrap_langs = "javascript,ruby,haskell,lisp,elixir,python,c,php,html,lua,ocaml,perl,go,erlang,java"
let g:vim_bootstrap_editor = "nvim"				" nvim or vim

if has('vim_starting')
  call s:source_rc('init.rc.vim')
endif

call s:source_rc('dein.rc.vim')

call s:source_rc('options.rc.vim')

call s:source_rc('visual.rc.vim')

call s:source_rc('abbreviations.rc.vim')

call s:source_rc('functions.rc.vim')

call s:source_rc('autocmd.rc.vim')

call s:source_rc('mappings.rc.vim')

call s:source_rc('variables.rc.vim')
